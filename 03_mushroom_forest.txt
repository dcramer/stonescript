var loadouts = import my/loadouts

func equip_melee()
  equipL sword *10
  equipR shield *10
  // equipR shield *5 poison

func equip_staff()
  equip staff *10 poison


?loc = fungus_forest
  ?hp = maxhp | foe.distance <= 23
    // Healed completely or enemy too close
    equip_staff()
  :
    // Try to heal while attacking from afar
    loadouts.equip_ranged_healing()
  ?hp <= 10
    activate potion
  loadouts.healwalk()

?loc = fungus_forest_boss  // Boss room
  ?foe = mushroom_boss_skinny  // Enoki
    equip_staff()
  :?foe = mushroom_boss // Angry or Morel
    ?foe.distance >= 6
      // Morel's hand keeps you at distance 5
      loadouts.equip_dashing()
    :?foe.armor > 0
      loadouts.equip_armor_piercing()
    :
      equip_melee()
  ?hp <= 10
    activate potion
  loadouts.healwalk()
