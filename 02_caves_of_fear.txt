/*
Copyright © 2019-2022, Matjaž Guštin
<dev@matjaz.it> <https://matjaz.it>
Released under the BSD 3-clause license.

Level: Caves of fear
*/

var loadouts = import my/loadouts

func equip_melee()
  equipL sword *10
  equipR shield *10

func equip_staff()
  equip staff *5 ice

func equip_ranged()
  equip repeating crossbow 6*

?loc = caves
  ?foe ! boss  // Small enemies
    ?hp < 10
      activate potion
    :?foe.name = hill | foe.name = snail
      equip_staff()
    :?hp ! maxhp & foe.distance > 12
      // Try to heal while attacking from afar
      loadouts.equip_ranged_healing()
    :?foe.distance > 10
      equip_ranged()
    :
      equip_melee()
  :?foe = boss  // Bolesh, the cunning
    ?foe.distance > 10
      loadouts.equip_dashing()
    :
      equip blade_of_god
      // equip_melee()
  :?hp < 10
    activate potion

  loadouts.healwalk()
